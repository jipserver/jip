// custom tasks for creating source jar
task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
// custom tasks for creating javadoc jar
task javadocJar(type: Jar, dependsOn:"javadoc") {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
// custom tasks for creating test jar
task testJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.test.output
}

/**
 * Write a properties file that contains version and build information
 */
task writeBuildProperties(){
    ext.bp = file("${sourceSets.main.output.resourcesDir.absolutePath}/${project.name}-build.properties")
    outputs.file file(bp)

    ext.props = [:]
    props["library.version"] = project(":").version
    props["build.version"] = gitVersion
    props["build.branch"] = gitBranch
    inputs.properties(props)

    doLast{
        if(!bp.exists()){
            bp.getParentFile().mkdirs();
            bp.createNewFile();
        }else{
            // delete old
            bp.delete()
            bp.createNewFile();
        }
        props["build.date"] = buildDate
        props.each{k,v->
            bp.append("${k}=${v}\n")
        }
    }
}
tasks.classes.dependsOn tasks.writeBuildProperties

task(versionInfo) << {
    description "Print version info"
    println "${project.name} Version info"
    println "Flux Lib:\t${version}"
    if(project(":").applications[project.name]){
        println "Flux App:\t${project(':').applications[project.name]}"
    }
    println "Build Date:\t${buildDate}"
    println "Build Version:\t${gitVersion}"
    println "Build Branch:\t${gitBranch}"
    println ""
}


configurations {
//    tests
    sjar
    jdoc
}

// add the jars as artifacts
artifacts {
    archives sourcesJar
    archives javadocJar
//    archives testJar
}
